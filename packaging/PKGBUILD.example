# Maintainer: Riaan de Beer
pkgname=katalogue-git
pkgver=1.1.0
pkgrel=1
pkgdesc="Modern Qt6/KDE disk catalog application"
arch=('x86_64')
url="https://github.com/infinityabundance/Katalogue"
license=('Apache-2.0')
depends=('qt6-base' 'qt6-declarative' 'qt6-svg' 'qt6-tools' 'kirigami' 'systemd' 'dbus')
makedepends=('cmake' 'ninja' 'git')
source=("$pkgname::git+https://github.com/infinityabundance/Katalogue.git")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  git describe --tags --always | sed 's/^v//;s/-/./g'
}

build() {
  cd "$srcdir/$pkgname"
  cmake -S katalogue -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

check() {
  cd "$srcdir/$pkgname"
  dbus-run-session -- ctest --test-dir build --output-on-failure -LE benchmark
}

package() {
  cd "$srcdir/$pkgname"
  DESTDIR="$pkgdir" cmake --install build
}
